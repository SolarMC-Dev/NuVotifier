
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.spotbugs" version "4.7.1" apply false
    id "net.kyori.blossom" version "1.3.0" apply false
}

allprojects {
    group 'gg.solarmc.votifier'
    version '3.0.0-SNAPSHOT'

    apply from: "${rootDir}/gradle/dependencies.gradle"
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: "com.github.spotbugs"

    apply from: "${rootDir}/gradle/publishing.gradle"

    sourceCompatibility = 16

    repositories {
        mavenCentral()
        maven {
            url "https://repo.solarmc.gg/mvn"
        }
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.4.2"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.4.2"
        testImplementation "org.junit.jupiter:junit-jupiter-params:5.4.2"
        testImplementation "org.mockito:mockito-core:2.+"
    }

    processResources {
        filter(ReplaceTokens, tokens: ["app.version": this.project.version])
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
        reports {
            html {
                enabled = true
                destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
                stylesheet = 'fancy-hist.xsl'
            }
        }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}
